/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_phaedriel.skrit

	Author(s)	: 	Bartosz Kijanka, Chad Queen

	Purpose		: 	Just stand there and talk at anyone we're told to.

	(C)opyright 2000 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInit$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	m_Target$	= job$.GoalObject;

	SetState Face$;
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}

	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		// $$$ read/write variables from the Game Auditor here, and bring up the appropriate dialogs...
		m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_companion_05_join" );

		if ( GameAuditor.GetDb.GetBool( "party_accept_0x1800e83" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_companion_05_accept_have_money" );
			GameAuditor.GetDb.SetBool( "party_accept_0x1800e83", false );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_accept_no_money_0x1800e83" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_companion_05_accept_no_money" );
			GameAuditor.GetDb.SetBool( "party_accept_no_money_0x1800e83", false );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x1800e83", true );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_decline_0x1800e83" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_companion_05_reject" );
			GameAuditor.GetDb.SetBool( "party_decline_0x1800e83", false );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x1800e83", true );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_disband_0x1800e83" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_companion_05_disband" );
			GameAuditor.GetDb.SetBool( "party_disband_0x1800e83", false );
			GameAuditor.GetDb.SetBool( "party_disbanded_0x1800e83", true );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_disbanded_0x1800e83" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_companion_05_disband_rejoin" );
			TryAnim$('talk');
		}
		else if ( GameAuditor.GetDb.GetBool( "party_allow_rejoin_0x1800e83" ) )
		{
			m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_companion_05_rejoin" );
			GameAuditor.GetDb.SetBool( "party_allow_rejoin_0x1800e83", false );
			TryAnim$('talk');
		}
		else
		{
			TryAnim$('talk');
		}

		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


