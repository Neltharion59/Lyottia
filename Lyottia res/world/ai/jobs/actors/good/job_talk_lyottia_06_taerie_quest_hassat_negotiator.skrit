Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInit$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	m_Target$	= job$.GoalObject;

	SetState Face$;
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}
	
	event OnEnterState$
	{
        // After step 4 - Quest finished - Hassat negotiator has been talked to the second time
        if ( GameAuditor.GetDb.GetBool( "lyottia_06_taerie_quest_after_step_4" ) )
        {
            m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_taerie_quest_negotiator_after_step_4" );
            GameAuditor.GetDb.SetBool( "lyottia_06_taerie_quest_negotiator_talked_to_after_step_4", true );
            TryAnim$('talk');
        }
        // After step 3 - Adventurer has been talked to
        else if ( GameAuditor.GetDb.GetBool( "lyottia_06_taerie_quest_after_step_3" ) )
        {
            m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_taerie_quest_negotiator_after_step_3" );
            GameAuditor.GetDb.SetBool( "lyottia_06_taerie_quest_negotiator_talked_to_after_step_3", true );
            GameAuditor.GetDb.SetBool( "lyottia_06_taerie_quest_after_step_4", true );
            TryAnim$('talk');
			DumpMain$; // drop quest reward
        }
        // After step 2 - Hassat has been talked to the first time
        else if ( GameAuditor.GetDb.GetBool( "lyottia_06_taerie_quest_after_step_2" ) )
        {
            m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_taerie_quest_negotiator_after_step_2" );
            GameAuditor.GetDb.SetBool( "lyottia_06_taerie_quest_negotiator_talked_to_after_step_2", true );
            TryAnim$('talk');
        }
        // After step 1 - Guard has been talked to
        else if ( GameAuditor.GetDb.GetBool( "lyottia_06_taerie_quest_after_step_1" ) )
        {
            m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_taerie_quest_negotiator_after_step_1" );
            GameAuditor.GetDb.SetBool( "lyottia_06_taerie_quest_negotiator_talked_to_after_step_1", true );
            GameAuditor.GetDb.SetBool( "lyottia_06_taerie_quest_after_step_2", true );
            TryAnim$('talk');
        }
        // Quest has not yet started
        else
        {
            if ( GameAuditor.GetDb.GetBool( "lyottia_06_taerie_quest_negotiator_talked_to_before_step_1" ) )
            {
                m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_taerie_quest_negotiator_before_step_1_next" );
                TryAnim$('talk');
            }
            else
            {
                m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_06_taerie_quest_negotiator_before_step_1_first" );
                GameAuditor.GetDb.SetBool( "lyottia_06_taerie_quest_negotiator_talked_to_before_step_1", true );
                TryAnim$('talk');
            }
        }

		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}

