[effect_script*]
{
	name	=lyottia_16_frostnid_freeze;

	script	=[[
            sound play s_e_ds2_gen_twstdshl_mag at #SOURCE_POSITION dist 10 30;

			set $size .3;
			
			sfx create orbiter #TARGET 
				"must_update()invisible()offset(0,.1,0)iphi(0)radius(0)[1]";
			set $orb #POP;
			sfx snap_to_ground $orb;
			sfx start $orb;

			/////////////////////////////////////////
			// This is the main trackball for the effect, everything is based off of this.
			sfx create trackball #TARGET_KB 
				"must_update()color0(.5,.5,1)invisible()no_collide_objects()
				accel(0)scale(1.2)velocity(9)afterlife(1)[1]";
			set $trackball #POP;
			sfx rat $trackball;
			sfx target $trackball $orb;
			sfx position_at $trackball @body_anterior source;
			sfx offset $trackball v<0 .8 0> source; 
			sfx offset_bone $trackball v<0 .8 0> target; 
			
			sfx create fire #TARGET
				"must_update()scale(1)count(30)color0(.5,.5,1)color1(0,0,0)alphafade(2)
				velocity(0,100,0)accel(0,0,0)flamesize(10)max_displace(14)[1]";
			set $fire2 #POP;
			sfx rat $fire2;
			sfx target $fire2 $trackball;
			sfx start $fire2;

			sfx create fireb #TARGET
				"must_update()fctrl(1,1,-1)scale(4)count(100)color0(.2,.3,.7)velocity(0,10,0)
				accel(0,-2,0)flamesize(2)alphafade(.25)[0][1]";
				 
			set $sparkle1 #POP;
			sfx rat $sparkle1;
			sfx target $sparkle1 $trackball;
			sfx friendly party $sparkle1;
			sfx start $sparkle1;

			sfx start $trackball;

			pause 2;
			
			sfx finish $sparkle1
			
			pause .5;
			
			sfx finish $fire2;
			
			pause .4;
			
			sfx finish $trackball;
			
			
			worldmsg WE_SPELL_SYNC_END #OWNER_GOID #OWNER_GOID #TARGET_GOID;
			]];
}
[effect_script*] // - ET
{
	name	=lyottia_16_fireball;

	script	=[[

			sound play s_e_spell_fireball_cast at #SOURCE_POSITION dist 3 20;			

			call lyottia_16_fireball_base v<0 0 0> u<1>;
			waitfor script #POP #DEFAULT_TIMEOUT;
			worldmsg WE_SPELL_SYNC_END #OWNER_GOID #OWNER_GOID #TARGET_GOID;

			]];
}

[effect_script*]
{
	name	=lyottia_16_fireball_base;

	script	=[[
            set $color .286,.600,1;

			/////////////////////////////////////////
			// This is the main trackball for the effcect, everything is based off of this.
			sfx create trackball #TARGET_KB 
				"must_update()accel(0)dur(4)afterlife(2)scale(.45)ts(1.4)
				color0($color)";
			set $trackball #POP;

			////////////////////
			// choose a different starting point and sound depending on what we are.
			if ( [1] == 3 )
			{// firestorm
				sfx position_at $trackball @kill_bone source;
			}
			else 
			{// fireblast or fireball
				sfx position_at $trackball @weapon_bone source;
			}

			if ( [1] != 3 )
			{// firestorm
				//////////////////////////////////////////
				// Create a light to attach to our trackball
				sfx create lightsource #SOURCE 
					"must_update()color0($color)color1($color)";
				set $firelight #POP;
				sfx rat $firelight;
				sfx target $firelight $trackball;
				sfx start $firelight;
			}

			//////////////////////////////////////////
			// create the flames, this is supposed to be a fireball after all.
			sfx create fire #SOURCE	
				"must_update()ts(2)count(20)scale(1.5)color0($color)alphafade(1)
				velocity(0,0,0)accel(0,0,0)min_displace(-.5)max_displace(.5)max_radius(4)
				flamesize(2)";
			set $fire #POP;
			sfx rat $fire;
			sfx target $fire $trackball;
			sfx start $fire;

			sfx create fire #SOURCE	
				"must_update()color0($color)count(15)ts(1.5)alphafade(.75)
				scale(.55)min_displace(-.5)max_displace(.5)accel(0,0,0)
				velocity(0,0,0)max_radius(8)texture(b_sfx_fireball-01)";
			set $fire2 #POP;
			sfx rat $fire2;
			sfx target $fire2 $trackball;
			sfx start $fire2;

			// we don't wan't to hit our friends.
			sfx friendly party $trackball;
			
			///////////////////////////////////////////
			// this adds some variance to where the trackball is aiming
			sfx offset_bone $trackball [0] target;
			sfx freeze_targets $trackball;

			// lets this baby fly.
			sfx start $trackball;

			sfx attach $trackball $fire;

			
//			sound play s_e_spell_fireball_cast at #SOURCE_POSITION dist 3 20;

			waitfor collision $trackball #DEFAULT_TIMEOUT;

			set $collision_type #POP;
			if ( ( $collision_type == #OBJECT_COLLISION ) || ( $collision_type == #TERRAIN_COLLISION ) )
			{
				////////////////////
				// choose a different sound and detail depending on what we are.

				if ( [1] == 1 )
				{// if we are a plain old fireball
					get target_position $trackball target;
					sound play s_e_spell_fireball_hit at #POP dist 3 20;
					set $scale .7;
					set $detail 20;
				}
				else
				{
					get target_position $trackball target;
					sound play s_e_spell_fireball_rain_hit at #POP dist 3 20;
					set $scale .4;
					set $detail 16;
				} // ( [1] == 1 )

				get collision point $trackball;
				sfx create cylinder #POP 
					"must_update()ts(2)offset(0,.05,0)scale($scale)texture(b_sfx_cyl_03)
					hp0(.2,.2,0)hp1(.2,.2,0)rp0(0,4,7)rp1(.75,4.75,8)tin(.10)tout(.5)dur(.35)
					color0($color)spin(15)segments($detail)";
				sfx start #POP;

				get collision point $trackball;
				sfx create cylinder #POP 
					"must_update()scale($scale)texture(b_sfx_cyl_03)hp0(.2,.2,0)hp1(.2,.2,0)
					rp0(0,3,4)rp1(.75,3.25,8)tin(.10)tout(.5)dur(.35)color0($color)spin(15)
					segments($detail)";
				sfx start #POP;

				get collision point $trackball;
				sfx create explosion #POP
					"must_update()radius(0)texture(b_sfx_snow_01)color0($color)color1(0,0,0)
					ivel(0,.25,0)vmin(6)vmax(8)omni_dir()fade_range(0,1,0)scale(.75)";
							
				sfx start #POP;

				//////////////////////////////////
				// create a little explosion where the ball hits.
				get collision point $trackball;
				sfx create explosion #POP 
					"must_update()scale(.8)vmin(6)vmax(8)texture(b_sfx_snow_01)
					count(16)color0($color)rvel(3,1,3)";
				set $explosion #POP;

				//////////////////////////////////
				// some tracer lookin things
				//get collision point $trackball
				//sfx create sray -must_update()ts(5)offset(0,.5,0)dur(.3)scale($scale)radius(.5)lmin(2)lmax(3)color0(0,0,0)color1($color)theta(0,0,0)phi(0,0,0)wsmin(0)wsmax(0)wemin(.1)wemax(.1) #POP
				//sfx start #POP

				////////////////////////////////////
				// create some light for the flames to give off.
				get collision point $trackball;
				sfx create lightsource #POP 
					"must_update()color0($color)color1($color)dur(2.5)";
				set $explodelight #POP;
				sfx start $explodelight;

				// create the fire in the center
				get collision point $trackball;
				sfx create orbiter #POP 
					"must_update()offset(0,.06,0)texture(b_sfx_sparkle01)
					scale(4)color0($color)radius(0)tin(0)tout(.2)dur(.2)";
				sfx start #POP;

				//////////////////////////////////////////////////////////////////////////
				// find out which way the ball hit and have the explosion go accordingly.
				get collision direction $trackball;
				sfx direction $explosion #POP;
				sfx start $explosion;


			}

			////////////////////////////
			// clean up
			sfx destroy $trackball;

			if ( [1] != 3 )
			{// firestorm
				sfx destroy $firelight;
			}
			
			pause 3;

			worldmsg WE_SPELL_SYNC_END #INVALID_GOID #OWNER_GOID #TARGET_GOID;

			]];
}
[effect_script*]
{
	name	=lyottia_16_fire_blast;

	script	=[[	
			
			sound play s_e_spell_fireball_cast at #SOURCE_POSITION dist 3 20;
			
			///////////////////////////////////////////////////////////////////////////////
			// we call the base fireball with some random offsets to make it shoot in a fan.
			
			frandrange -3 3; frandrange -3 3;
			frandrange -3 3; frandrange -3 3;
			frandrange -3 3; frandrange -3 3;
			frandrange -3 3; frandrange -3 3;
			frandrange -3 3; frandrange -3 3;
			frandrange -3 3; frandrange -3 3;
			frandrange -3 3; frandrange -3 3;
			frandrange -3 3; frandrange -3 3;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot1 #POP;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot2 #POP;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot3 #POP;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot4 #POP;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot5 #POP;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot6 #POP;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot7 #POP;

			call lyottia_16_fireball_base v<#POP 0 #POP> u<2>;
			set $shot8 #POP;

			call lyottia_16_fireball_base v<0 0 0> u<2>;
			set $shot9 #POP;

			worldmsg WE_SPELL_SYNC_END #OWNER_GOID #OWNER_GOID #TARGET_GOID;

			]];
}
[effect_script*]
{
	name	=lyottia_16_dragon_fire;

	script	=[[
			sound play s_e_dragon_fire at #TARGET_POSITION dist 3 25;
		
			set $velocity 0,0,40;
			set $accel 0,0,0;

			set $flames1 60;
			set $flames2 80;

			sfx create fireb #SOURCE 
				"offset(0,.2,.5)fctrl(0,0,7)flamesize(1)alphafade(.5)ts(.6)color0(.28,.6,1)
				velocity($velocity)accel($accel)cvel(1)lower_r0(0)lower_r1(0)
				upper_r0(0)upper_r1(20)count(80)texture(b_sfx_fireball-01)max_displace(2)min_displace(-2)";
			sfx friendly party #PEEK;
			set $fire1 #PEEK;
			sfx start #POP;

			sfx create fireb #SOURCE 
				"offset(0,.2,.5)fctrl(0,0,7)flamesize(2)alphafade(.5)ts(.6)color0(.28,.6,1)
				velocity($velocity)accel($accel)cvel(1)lower_r0(0)lower_r1(0)start_rate(10)
				upper_r0(0)upper_r1(20)count(30)texture(b_sfx_fireball-02)max_displace(3)min_displace(-3)";
			sfx friendly party #PEEK;
			set $fire2 #PEEK;
			sfx start #POP;

			sfx create fireb #SOURCE 
				"offset(0,.2,.5)fctrl(0,0,0)flamesize(1)alphafade(.5)ts(1.6)color0(.28,.6,1)
				velocity(-10,0,5)accel(0,0,-10)cvel(1)lower_r0(0)lower_r1(0)max_displace(3)min_displace(-3)
				upper_r0(10)upper_r1(20)count(30)start_rate(10)texture(b_sfx_fireball-01)";
			sfx friendly party #PEEK;
			set $fire3 #PEEK;
			sfx start #POP;
			
			sfx create fireb #SOURCE 
				"offset(0,.2,.5)fctrl(0,0,0)flamesize(1)alphafade(.5)ts(1.6)color0(.28,.6,1)
				velocity(10,0,5)accel(0,0,-10)cvel(1)lower_r0(0)lower_r1(0)max_displace(3)min_displace(-3)
				upper_r0(10)upper_r1(20)count(30)start_rate(10)texture(b_sfx_fireball-01)";
			sfx friendly party #PEEK;
			set $fire4 #PEEK;
			sfx start #POP;
			
			sfx create fireb #SOURCE 
				"offset(0,.2,.5)fctrl(0,0,2)flamesize(.1)alphafade(.45)ts(.6)color0(.28,.6,1)
				velocity($velocity)accel($accel)cvel(1)lower_r0(4)lower_r1(4)max_displace(6)min_displace(-6)
				upper_r0(10)upper_r1(30)count(80)texture(b_sfx_sparkle01)[0]";
			sfx friendly party #PEEK;
			set $fire5 #PEEK;
			sfx start #POP;
			
			sfx create fireb #SOURCE 
				"offset(0,.2,.5)fctrl(0,0,2)flamesize(.2)alphafade(.45)ts(.6)color0(0,0,0)
				velocity($velocity)accel($accel)cvel(1)lower_r0(3)lower_r1(3)max_displace(3)min_displace(-3)
				upper_r0(10)upper_r1(30)count(80)dark()";
			sfx friendly party #PEEK;
			set $fire6 #PEEK;
			sfx start #POP;
			
			pause [2];
			
			sfx finish $fire1;
			sfx finish $fire2;
			sfx finish $fire3;
			sfx finish $fire4;
			sfx finish $fire5;
			sfx finish $fire6;
			
			pause 6.5;
			
			worldmsg WE_SPELL_SYNC_END #INVALID_GOID #OWNER_GOID #TARGET_GOID;
			]];
}