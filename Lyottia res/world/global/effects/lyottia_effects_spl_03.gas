[effect_script*] // -ET
{
	name	=lyottia_03_effects_bombard;

	script	=[[
			///////////////////////////////////////////////////////////////////////////////
			// we call the base firebomb with some random offsets to make it shoot all over.

			call lyottia_03_effects_firebomb_base v<0 0 0> u<2>;
			set $shot1 #POP;

			waitfor script $shot1 #DEFAULT_TIMEOUT;
			
			worldmsg WE_SPELL_SYNC_END #OWNER_GOID #OWNER_GOID #TARGET_GOID;

			]];
}
[effect_script*] // -ET
{
	name	=lyottia_03_effects_bombard_multiple;

	script	=[[
			///////////////////////////////////////////////////////////////////////////////
			// we call the base firebomb with some random offsets to make it shoot all over.

			call lyottia_03_effects_firebomb_base v<0 0 0> u<2>;
			set $shot1 #POP;

			call lyottia_03_effects_firebomb_base v<5 0 5> u<2>;
			set $shot2 #POP;

			call lyottia_03_effects_firebomb_base v<5 0 -5> u<2>;
			set $shot1 #POP;

			call lyottia_03_effects_firebomb_base v<-5 0 5> u<2>;
			set $shot2 #POP;

			call lyottia_03_effects_firebomb_base v<-5 0 -5> u<2>;
			set $shot1 #POP;

			waitfor script $shot1 #DEFAULT_TIMEOUT;
			
			worldmsg WE_SPELL_SYNC_END #OWNER_GOID #OWNER_GOID #TARGET_GOID;

			]];
}
[effect_script*] // - ET
{
	name	=lyottia_03_effects_firebomb_base;

	script	=[[
	
			sfx create orbiter #TARGET_KB 
				"radius(0)dur(10)invisible()";
			sfx snap_to_ground #PEEK;              
			set $orb #PEEK;
			sfx start #POP;  
			
			pause .01;                                     
	        
			/////////////////////////////////////////
			// This is the main trackball for the effect, everything is based off of this.
			sfx create trackball #TARGET_KB 
				"must_update()dir(0,1,0)dur(#DEFAULT_TIMEOUT)afterlife(2)scale(.45)
				ts(1.4)homing()damped()kdamp(0)accel(1)color0(0,.2,.9)";
			sfx rat #PEEK;
			sfx target #PEEK $orb;
			set $trackball #POP;
			sfx position_at $trackball @weapon_bone source;
			sfx offset_bone $trackball [0] target;
//			sfx freeze_targets $trackball;
			sfx friendly party $trackball;
			sfx start $trackball;
			
			pause .01;                                     
	        
			////////////////////
			// choose a different starting point if we are fireball storm.
			if ( [1] == 3 )
			{
				sfx position_at $trackball @kill_bone source;
			}
			else
			{
				sfx position_at $trackball @weapon_bone source;
			} // ( [1] == 3 )

			//////////////////////////////////////////
			// Create a light to attach to our trackball
			sfx create lightsource #SOURCE 
				"must_update()color0(0,.2,.9)color1(0,0,1)";
			set $firelight #POP;
			sfx rat $firelight;
			sfx target $firelight $trackball;
			sfx start $firelight;


			//////////////////////////////////////////
			// create the flames, the is supposed to be a fireball after all.
			sfx create fire #SOURCE
				"must_update()count(20)color0(0,.2,.9)ts(1.5)
				velocity(0,0,0)accel(0,0,0)alphafade(1)scale(.5)max_radius(10)
				min_displace(-1)max_displace(1)bone_orient()texture(b_sfx_blueflare_02)";	
			set $fire #POP;
			sfx rat $fire;
			sfx target $fire $trackball;
			sfx start $fire;
			

			sfx create fire #SOURCE
				"must_update()count(30)color0(0,.2,.9)ts(2.5)
				velocity(0,0,0)accel(0,0,0)alphafade(1)scale(1.33)max_radius(3)
				min_displace(-1)max_displace(1)bone_orient()texture(b_sfx_blueflare_02)";	
			set $fire2 #POP;
			sfx rat $fire2;
			sfx target $fire2 $trackball;
			sfx start $fire2;
			
			sound play s_e_spell_lightning_storm_cast at #SOURCE_POSITION dist 3 20;
			waitfor collision $trackball #DEFAULT_TIMEOUT;
			
			set $collision_type #POP;
			if ( ( $collision_type == #OBJECT_COLLISION ) || ( $collision_type == #TERRAIN_COLLISION ) )
			{
				get target_position $trackball target;
				sound play s_e_spell_lightning_storm_hit at #POP dist 3 20;
				
				if ( [1] == 1 )
				{// if we are a plain old fireball
					set $scale .7;
					set $detail 20;
				}
				else
				{
					set $scale .4;
					set $detail 16;
				} // ( [1] == 1 )
	
				frandrange 10 40; frandrange 10 40; set $r1a #POP; set $r2a #POP;
				frandrange 10 40; frandrange 10 40; set $r1b #POP; set $r2b #POP;
				frandrange 10 40; frandrange 10 40; set $r1c #POP; set $r2c #POP;
	
				get collision point $trackball;
				sfx create sphere #POP 
					"must_update()scale($scale)color0(0,.2,.9)dur(.5)tin(.2)
					tout(.5)irotate($r1a,$r1b,$r1c)grow_params(.1,1.5,3)";
				sfx start #POP;
	
				get collision point $trackball;
				sfx create sphere #POP 
					"must_update()scale($scale)color0(0,.2,.9)dur(.5)tin(.2)
					tout(.5)irotate($r2a,$r2b,$r2c)grow_params(.1,1.5,3)";
				sfx start #POP;
	
				get collision point $trackball;
				sfx create cylinder #POP 
					"must_update()scale($scale)texture(b_sfx_cyl_01)hp0(.1,.1,.1)
					hp1(.1,.1,.1)rp0(0,8,5)rp1(.75,8.75,8)tin(.20)tout(.5)
					dur(.65)color0(0,.2,.9)spin(15)segments($detail)";
				sfx start #POP;
	
				//////////////////////////////////
				// create a little explosion where the ball hits.
				get collision point $trackball;
				sfx create explosion #POP 
					"must_update()count($detail)color0(0,.2,.9)rvel(8,0,8)
					vmin(2)vmax(4)";
				sfx start #POP;
	
				get collision point $trackball;
				sfx create explosion #POP 
					"must_update()count($detail)color0(0,.2,.9)rvel(8,0,8)
					texture(b_sfx_sparkle01)vmin(2)vmax(4)";
				sfx start #POP;
	
				get collision point $trackball;
				sfx create flurry #POP
					"must_update()count(30)tin(0)tout(.5)dur(0.5)color0(0,.2,.9)
					grow_params(-.3,1.4,1.7)scale(1.35)delay(.07)";
				sfx start #POP;
	
				//////////////////////////////////
				// some tracer lookin things
				get collision point $trackball;
				sfx create sray #POP 
					"must_update()count($detail)ts(5)offset(0,.5,0)dur(.2)
					scale($scale)radius(.5)lmin(2)lmax(5)color0(0,0,0)color1(0,.2,.9)
					theta(0,0,0)phi(0,0,0)wsmin(0)wsmax(0)wemin(.1)wemax(.1)";
				sfx start #POP;
				
				////////////////////////////
				// clean up
				sfx destroy $trackball;
			}
			
			worldmsg WE_SPELL_SYNC_END #INVALID_GOID #OWNER_GOID #TARGET_GOID;
						
			]];
}