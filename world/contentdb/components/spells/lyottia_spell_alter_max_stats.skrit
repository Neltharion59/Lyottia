//////////////////////////////////////////////////////////////////////////////
//
// File     :  lyottia_spell_deathrain_line.skrit
// Author(s):  Emir Ghanawa
//
//----------------------------------------------------------------------------
//  $Revision:: $              $Date:$
//----------------------------------------------------------------------------
//////////////////////////////////////////////////////////////////////////////

property string	effect_script$		= ""	doc = "Effect script ot give the spell some visual";
property string effect_params$		= ""   	doc = "Parameters to send to SiegeFx script";

property bool	update_max_life$		   	= true  	doc = "Should max life be modified?";
property float	max_life_bonus$		   	= 10.0  	doc = "How much to modify max life?";

property bool	update_current_life$		   	= true  	doc = "Should current life also be increased?";

property float	max_life_cap_upper$		   	= 100.0  	doc = "Max life should not be higher than this";
property bool	apply_max_life_cap_upper$		   	= false  	doc = "Should upper cap be applied?";

property float	max_life_cap_lower$		   	= 10.0  	doc = "Max life should not be lower than this";
property bool	apply_max_life_cap_lower$		   	= false  	doc = "Should lower cap be applied?";

owner 	= GoSkritComponent;


startup state Idle$
{
	event OnGoHandleMessage$( eWorldEvent e$, WorldMessage msg$ )
	{
		if( e$ == WE_REQ_CAST )
		{
			Goid target$ = MakeGoid( msg$.GetData1() );
			Goid caster$ = msg$.GetSendFrom();

			if( !target$.IsValid || !caster$.IsValid )
			{
				SetState Finished$;
				return;
			}

            if( effect_script$ != "" )
			{
				SiegeFx.SRunMpScript( effect_script$, target$, caster$, effect_params$, owner.Goid, e$ );
			}
			
			if (update_max_life$)
            {
                float current_max_life$ = target$.Go.aspect.GetMaxLife();
                float new_max_life$ = current_max_life$;

                if (apply_max_life_cap_upper$ && max_life_bonus$ > 0.0)
                {
                    if (current_max_life$ < max_life_cap_upper$)
                    {
                        new_max_life$ = Math.MinFloat(max_life_cap_upper$, current_max_life$ + max_life_bonus$);
                    }
                }
                else if (apply_max_life_cap_lower$ && max_life_bonus$ < 0.0)
                {
                    if (current_max_life$ < max_life_cap_upper$)
                    {
                        new_max_life$ = Math.MaxFloat(max_life_cap_lower$, current_max_life$ + max_life_bonus$);
                    }
                }
                else
                {
                    new_max_life$ = Math.MaxFloat(1.0, current_max_life$ + max_life_bonus$);
                }

                if (new_max_life$ != current_max_life$)
                {
                    target$.Go.aspect.SSetMaxLife(new_max_life$);
    
                    float current_life$ = target$.Go.aspect.CurrentLife;
                    float new_current_life$ = current_life$;
                    if (new_max_life$ < current_max_life$)
                    {
                        new_current_life$ = Math.MinFloat(current_life$, new_max_life$);
                    }

                    if (update_current_life$)
                    {
                        if (new_max_life$ > current_max_life$)
                        {
                            new_current_life$ = new_current_life$ + (new_max_life$ - current_max_life$);
                        }
                    }
                    
                    if (new_current_life$ != current_life$)
                    {
                        target$.Go.aspect.SSetCurrentLife(new_current_life$);
                    }
                }
            }
		}
	}
}


state Finished$
{
	event OnEnterState$
	{
		// Get rid of any temporary storage
		SiegeFx.ClearVariables( owner.goid );

		// Since this skrit is the component of a spell and all spells are cloned before being executed
		// then it is safe to destroy ourself after finishing execution

		PostWorldMessage( WE_REQ_DELETE, owner.Goid, owner.Goid, 5.1 );
	}
}