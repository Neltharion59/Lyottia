
Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInit$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	m_Target$	= job$.GoalObject;

	SetState Face$;
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}
	
	event OnEnterState$
	{
        if ( GameAuditor.GetDb.GetBool( "lyottia_17_goblin_quest_complete" ) )
        {
            if ( GameAuditor.GetDb.GetBool( "lyottia_17_goblin_talked_to_after_quest" ) )
            {
                m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_17_goblin_after_quest_next" );
            }
            else
            {
                m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_17_goblin_after_quest_first" );
                GameAuditor.GetDb.SetBool( "lyottia_17_goblin_talked_to_after_quest", true );
				DumpMain$; // drop quest reward
            }
        }
        else if
        (
            GameAuditor.GetDb.GetBool( "lyottia_17_goblin_talked_to_b4_quest" )
            && Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "lyottia_qi_dead_goblin", m_target$ ) 
        )
        {
            m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_17_goblin_ask_for_item" );
        }
        else
        {
            if ( GameAuditor.GetDb.GetBool( "lyottia_17_goblin_talked_to_b4_quest" ) )
            {
                m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_17_goblin_b4_quest_next" );
            }
            else
            {
                m_Go$.GetConversation.RSSelectConversation( "lyottia_conversation_17_goblin_b4_quest_first" );
                GameAuditor.GetDb.SetBool( "lyottia_17_goblin_talked_to_b4_quest", true );
            }
        }

		TryAnim$('talk');
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		// $$$ close up any dialogs here...

		// Send a PL_FACE reguest without a target so that they no longer
		// continue to face the character they were talking to
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

		m_Job$.MarkForDeletion();
	}
}


